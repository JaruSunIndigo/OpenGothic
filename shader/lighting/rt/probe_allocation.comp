#version 450

#extension GL_EXT_control_flow_attributes : enable
#extension GL_GOOGLE_include_directive    : enable

#include "lighting/rt/probe_common.glsl"
#include "lighting/tonemapping.glsl"
#include "scene.glsl"
#include "common.glsl"

#if defined(CLEAR) || defined(GC)
layout(local_size_x = 64) in;
#else
layout(local_size_x = 8, local_size_y = 8) in;
#endif

const int MAX_DEBUG_COLORS = 10;
const vec3 debugColors[MAX_DEBUG_COLORS] = {
  vec3(1,1,1),
  vec3(1,0,0),
  vec3(0,1,0),
  vec3(0,0,1),
  vec3(1,1,0),
  vec3(1,0,1),
  vec3(0,1,1),
  vec3(1,0.5,0),
  vec3(0.5,1,0),
  vec3(0,0.5,1),
  };

layout(binding = 0, std140) uniform UboScene {
  SceneDesc scene;
  };

layout(binding = 1) uniform sampler2D diffuse;
layout(binding = 2) uniform sampler2D normals;
layout(binding = 3) uniform sampler2D depth;
layout(binding = 4, std430) buffer          Hbo0 { uint voteTable[]; };
layout(binding = 5, std430) readonly buffer Hbo  { Hash hashTable[]; };
layout(binding = 6, std430) buffer          Pbo  { ProbesHeader probeHeader; Probe probe[]; };
layout(binding = 7, std430) buffer          Fbo0 { int cursor; uint list[]; } freeList;

ivec2 screenSize, fragCoord;
uint  threadId;

vec3 unprojectDepth(const float z) {
  const vec2  inPos = vec2(2*fragCoord+ivec2(1,1))/vec2(screenSize)-vec2(1,1);
  const vec4  pos   = vec4(inPos.xy,z,1);
  const vec4  ret   = scene.viewProjectInv*pos;
  return (ret.xyz/ret.w);
  }

uint allocProbe() {
  int cursor = atomicAdd(freeList.cursor, -1) - 1;
  if(cursor>=0)
    return freeList.list[cursor];
  return atomicAdd(probeHeader.count, 1);
  }

bool reuseProbe(const uint h, vec3 pos) {
  const uint probeId = hashTable[h].value;
  if(probeId>=probeHeader.count)
    return false;

  Probe p = probe[probeId];
  const vec3 dp = (p.pos - pos);
  if(dot(dp,dp)>1.0 || (p.bits & TRACED_BIT)==0)
    return false; // NOTE: reconstructed position may jitter a little

  probe[probeId].bits |= REUSE_BIT;
  probe[probeId].pNext = 0xFFFFFFFF;
  return true;
  }

void processProbe(ivec3 gridPos, vec3 pos, int lod, vec3 pixelPos, vec3 pixelNorm) {
  const uint h = probeGridPosHash(gridPos) % hashTable.length();
#if defined(VOTE)
  atomicMin(voteTable[h], threadId);
#elif defined(REUSE)
  if(voteTable[h] != threadId)
    return;
  if(reuseProbe(h, pos)) {
    voteTable[h] = 0xFFFFFFFF;
    return;
    }
#elif defined(ALLOC)
  if(voteTable[h] != threadId)
    return;
  voteTable[h] = 0xFFFFFFFF;

  const uint cursor = allocProbe();
  if(cursor>=probe.length())
    return;
  Probe p;
  p.pos         = pos;
  p.bits        = 0;
  p.pNext       = 0xFFFFFFFF;
  probe[cursor] = p;
#endif
  }

void main_process() {
  fragCoord  = ivec2(gl_GlobalInvocationID.xy);
  screenSize = ivec2(textureSize(depth,0));
  threadId   = fragCoord.x + fragCoord.y*screenSize.x;

  const bool valid = all(lessThan(fragCoord, screenSize));
  if(!valid)
    return;

  vec3  norm = normalize(texelFetch(normals,fragCoord,0).xyz*2.0-vec3(1.0));
  float z    = texelFetch(depth,fragCoord,0).x;
  if(z>=0.99995)
    return; // sky

  const float dist = linearDepth(z, scene.clipInfo);
  const vec3  pos  = unprojectDepth(z) + norm*probeCageBias;
  const int   lod  = probeGridLodFromDist(dist);

  probeQuery pq;
  probeQueryInitialize(pq, pos, lod);
  while(probeQueryProceed(pq)) {
    vec3 wpos = probeQueryWorldPos(pq);
    vec3 dir  = (wpos-pos);
    if(dot(dir,norm) < -0.1)
      continue;

    ivec3 gPos = probeQueryGridPos(pq);
    processProbe(gPos, wpos, lod, pos, norm);
    }
  }

void main_clear() {
  const uint threadID = gl_GlobalInvocationID.x;

  if(threadID<voteTable.length()) {
    voteTable[threadID] = 0xFFFFFFFF;
    }

  if(threadID==0) {
    probeHeader.count       = min(probeHeader.count, probe.length());
    probeHeader.iterator    = 0;
    probeHeader.tracedCount = 0;
    freeList.cursor         = 0;
    }
  }

void main_gc() {
  const uint probeId = gl_GlobalInvocationID.x;
  if(probeId>=probeHeader.count)
    return;

  probe[probeId].pNext = 0xFFFFFFFF;

  if((probe[probeId].bits & REUSE_BIT)!=0)
    return;

  probe[probeId].bits |= UNUSED_BIT;
  const uint cursor = atomicAdd(freeList.cursor, 1);
  freeList.list[cursor] = probeId;
  }

void main() {
#if defined(CLEAR)
  main_clear();
#elif defined(GC)
  main_gc();
#else
  main_process();
#endif
  }
