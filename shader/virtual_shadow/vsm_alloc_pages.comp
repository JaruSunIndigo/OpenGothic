#version 450

#extension GL_GOOGLE_include_directive    : enable
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_samplerless_texture_functions : enable

#include "virtual_shadow/vsm_common.glsl"
#include "scene.glsl"
#include "common.glsl"

layout(local_size_x = 32, local_size_y = 32) in;

layout(binding = 0, std430) buffer Pages { VsmHeader header; uint  pageList[]; } vsm;
layout(binding = 1, r32ui)  uniform uimage3D pageTbl;
layout(binding = 2, r32ui)  uniform uimage2D dbg;

const uint NumThreads = gl_WorkGroupSize.x*gl_WorkGroupSize.y*gl_WorkGroupSize.z;

shared uint pageListSize;
shared uint pageList[VSM_MAX_PAGES];

shared uint bits[(NumThreads+31)/32];

shared uint ladder  [VSM_PAGE_TBL_SIZE];
shared uint ladderWr[VSM_PAGE_TBL_SIZE];

void storePage(ivec2 pId, ivec3 at, ivec2 tile, ivec2 size) {
  const uint pageId = pId.x + pId.y*VSM_PAGE_PER_ROW;

  vsm.pageList[pageId] = packVsmPageInfo(at, size);
  atomicMax(vsm.header.pageCount, pageId+1);

  at += ivec3(tile,0);
  const uint prime = imageLoad(pageTbl, at).r & 0x1;
  imageStore(pageTbl, at, uvec4((pageId << 16u) | prime));

  imageStore(dbg, pId, uvec4(size.y));
  //imageStore(dbg, pId, uvec4(1));
  }

uint bitCountShared(uint till) {
  uint n = till/32;
  uint f = till%32;
  uint r = 0;
  for(uint i=0; i<n; ++i)
    r += bitCount(bits[i]);
  r += bitCount(bits[n] & ((1u << f)-1u));
  return r;
  }

void listPages() {
  const uint lane = gl_LocalInvocationIndex;

  pageListSize = vsm.header.pageCount;
  for(uint i=lane; i<pageListSize; i+=NumThreads)
    pageList[i] = vsm.pageList[i];
  barrier();
  }

ivec2 findRowCol(in uint page, const uvec2 size) {
  // const uint perRow = 32/size.x;
  // const uint row    = (page/perRow)*size.y;
  // const uint col    = (page%perRow)*size.x;
  // return ivec2(col,row);

  for(uint i=0; i<32; i+=size.y) {
    uint space = (32-ladder[i])/size.x;
    if(page<space)
      return ivec2(ladder[i] + page*size.x, i);
    page -= space;
    }
  // error
  return ivec2(-1);
  }

void layoutPages(uint pageData, uvec2 pageSz, uvec2 size) {
  const uint lane = gl_LocalInvocationIndex;

  pageListSize = 0;
  if(lane < bits.length())
    bits[lane] = 0;
  if(lane < ladderWr.length())
    ladder[lane] = ladderWr[lane];
  barrier();

  if(pageSz==size)
    atomicOr(bits[lane/32], 1u<<(lane%32));
  barrier();

  if(pageSz==size) {
    uint id = bitCountShared(lane);
    pageList[id] = pageData;
    atomicMax(pageListSize, id+1);
    }
  barrier();

  const uint perRow = VSM_PAGE_TBL_SIZE/size.x;
  const uint maxPix = pageListSize*(size.x*size.y);
  // const uint maxPix = 1*(size);
  for(uint i=lane; i<maxPix; i+=NumThreads) {
    const uint  page = i / (size.x*size.y);
    const uint  tile = i % (size.x*size.y);
    const ivec2 rc   = findRowCol(page, size);
    if(rc.x<0)
      break;

    const ivec3 at = unpackVsmPageInfo(pageList[page]);
    const ivec2 tx = ivec2(tile/size.y, tile%size.y);
    storePage(rc+tx, at, tx, tile==0 ? ivec2(size) : ivec2(0));
    atomicMax(ladderWr[rc.y+tx.y], rc.x + tx.x + 1);
    }
  barrier();
  }

void main() {
  const ivec3 at   = ivec3(gl_GlobalInvocationID);
  const ivec3 id   = ivec3(gl_LocalInvocationID);
  const uint  lane = gl_LocalInvocationIndex;
  if(lane < ladderWr.length())
    ladderWr[lane] = 0;
  barrier();

  imageStore(dbg, at.xy, uvec4(0));
  memoryBarrierImage();
  barrier();

  listPages();

  if(lane < vsm.pageList.length())
    vsm.pageList[lane] = 0;
  memoryBarrierBuffer();
  barrier();

  const uint  frag = lane<pageListSize ? pageList[lane] : 0;
  const uvec2 size = unpackVsmPageSize(frag);
  barrier();

  layoutPages(frag, size, uvec2(4));
  layoutPages(frag, size, uvec2(1, 4));
  layoutPages(frag, size, uvec2(2));
  layoutPages(frag, size, uvec2(1, 2));
  layoutPages(frag, size, uvec2(1));
  }
